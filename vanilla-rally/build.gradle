apply plugin: 'com.android.application'

android {
    compileSdk 36
    ndkVersion '29.0.13599879 rc2'

    defaultConfig {
        applicationId = 'com.hallo2hallo.vanillarally'
        minSdk 21
        targetSdk 36
        versionCode 56
        versionName '0.56'
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                // https://issuetracker.google.com/issues/37128614
                // "Android has always built debuggable release binaries and then stripped them
                // before they reach the APK. Once Studio starts using RELWITHDEBINFO,
                // we can make the change."
                // The "build" variant requires signing the APK, so use "Release" and let
                // Android Studio add the "-g" flag.
                arguments '-DANDROID_TOOLCHAIN=clang',
                          '-DANDROID_STL=c++_shared',
                          '-DWITH_OPENSSL=OFF',
                          '-DBUILD_TRIANGLE=OFF',
                          '-DBUILD_CV=OFF',
                          '-DBUILD_SFM=OFF',
                          '-DBUILD_OPENCV=OFF',
                          '-DCMAKE_BUILD_TYPE=Release'
                          // '--graphviz=/tmp/vanilla-rally.dot'
            }
        }
    }
    buildFeatures {
        buildConfig = true
    }
    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            version '3.22.1'
            path '../../CMakeLists.txt'
        }
    }
    flavorDimensions += "version"
    productFlavors {
        register("rally") {
            // Assigns this product flavor to the "version" flavor dimension.
            // If you are using only one dimension, this property is optional,
            // and the plugin automatically assigns all the module's flavors to
            // that dimension.
            dimension = "version"
            applicationIdSuffix = ""
            versionNameSuffix = ""
        }
        register("extended") {
            dimension = "version"
            applicationIdSuffix = ".extended"
            versionNameSuffix = "-extended"
        }
    }
    // https://stackoverflow.com/questions/9206117/how-to-workaround-auto-omitting-files-folders-starting-with-underscore-in
    androidResources {
        ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
    }
    // sourceSets {
    //     main {
    //         // let gradle pack the shared library into apk
    //         jniLibs.srcDirs = ['build/intermediates/merged_native_libs/debug/out/lib']
    //     }
    // }
    namespace 'com.hallo2hallo.vanillarally'
    buildToolsVersion '36.0.0'
}

dependencies {
    implementation(platform('org.jetbrains.kotlin:kotlin-bom:2.2.0'))
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation project(':recast')
}
